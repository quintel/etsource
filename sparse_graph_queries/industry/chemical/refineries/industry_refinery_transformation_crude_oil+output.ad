# ## Overview
# This query calculates the output conversion factors for the refinery transformation 
# process based on dataset inputs.
#
# ## Important Notes
# 1. The values given to output slots in datasets where there is 
#    no crude oil input are dummy/fallback values.
# 2. These values will be overwritten when refinery demand is 
#    configured in the input `industry_useful_demand_for_chemical_refineries`.
# 3. Uses actual dataset ratios when crude oil input > 0, 
#    otherwise falls back to predefined conversion factors.
- query =
    diesel_output = DATASET_INPUT(input_industry_refinery_transformation_crude_oil_diesel_demand);
    gasoline_output = DATASET_INPUT(input_industry_refinery_transformation_crude_oil_gasoline_demand);
    heavy_fuel_oil_output = DATASET_INPUT(input_industry_refinery_transformation_crude_oil_heavy_fuel_oil_demand);
    kerosene_output = DATASET_INPUT(input_industry_refinery_transformation_crude_oil_kerosene_demand);
    lpg_output = DATASET_INPUT(input_industry_refinery_transformation_crude_oil_lpg_demand);
    crude_oil_output = DATASET_INPUT(input_industry_refinery_transformation_crude_oil_other_oil_demand);
    refinery_gas_output = DATASET_INPUT(input_industry_refinery_transformation_crude_oil_refinery_gas_demand);
    naphtha_output =  DATASET_INPUT(input_industry_refinery_transformation_crude_oil_naphtha_demand);
    total_product_output = diesel_output + gasoline_output + heavy_fuel_oil_output + kerosene_output + lpg_output + crude_oil_output + refinery_gas_output + naphtha_output;
    crude_oil_input = DATASET_INPUT(industry_useful_demand_for_chemical_refineries_crude_oil_non_energetic_demand);

    IF(crude_oil_input > 0.0, {
        diesel: diesel_output / crude_oil_input,
        gasoline: gasoline_output / crude_oil_input,
        heavy_fuel_oil: heavy_fuel_oil_output / crude_oil_input,
        kerosene: kerosene_output / crude_oil_input,
        lpg: lpg_output / crude_oil_input,
        crude_oil: crude_oil_output / crude_oil_input,
        refinery_gas: refinery_gas_output / crude_oil_input,
        naphtha: naphtha_output / crude_oil_input,
        loss: 1 - (total_product_output / crude_oil_input)},
        {
        diesel: 0.1,
        gasoline: 0.1,
        heavy_fuel_oil: 0.1,
        kerosene: 0.1,
        naphtha: 0.1,
        lpg: 0.1,
        crude_oil: 0.2,
        refinery_gas: 0.2}
    )

