# If only the FLH is changed, the production is not changed by ETEngine. Instead, the NoU decreases,
# effectively decreasing total installed capacity. Therefore, first the original NoU is fetched,
# then the FLH is changed, the NoU is set to the original value and, finally, the preset demand by
# electricity production is updated.
# Both the FLH of dedicated hydrogen solar PV and the electrolyser are updated.

- query =
    IF(
      EQUALS(AREA(weather_curve_set), "default"),
      -> {
        original_nou_power_solar_pv =                 V(energy_power_solar_pv_solar_radiation,          number_of_units);
        original_nou_battery_solar_pv =               V(energy_battery_solar_pv_solar_radiation,        number_of_units);
        original_nou_hydrogen_solar_pv =              V(energy_hydrogen_solar_pv_solar_radiation,       number_of_units);
        original_nou_hydrogen_electrolysis_solar_pv = V(energy_hydrogen_electrolysis_solar_electricity, number_of_units);

        electrolyser_pv_flh_ratio =
          DIVIDE(
            V(energy_hydrogen_electrolysis_solar_electricity, full_load_hours),
            V(energy_hydrogen_solar_pv_solar_radiation, full_load_hours)
          );

        EACH(
          UPDATE(V(energy_power_solar_pv_solar_radiation), full_load_hours, USER_INPUT()),
          UPDATE(V(energy_power_solar_pv_solar_radiation), number_of_units, original_nou_power_solar_pv),
          UPDATE(V(energy_power_solar_pv_solar_radiation), preset_demand_by_electricity_production, V(energy_power_solar_pv_solar_radiation, production_based_on_number_of_units)),

          UPDATE(V(energy_battery_solar_pv_solar_radiation), full_load_hours, USER_INPUT()),
          UPDATE(V(energy_battery_solar_pv_solar_radiation), number_of_units, original_nou_battery_solar_pv),
          UPDATE(V(energy_battery_solar_pv_solar_radiation), preset_demand_by_electricity_production, V(energy_battery_solar_pv_solar_radiation, production_based_on_number_of_units)),

          UPDATE(V(energy_hydrogen_solar_pv_solar_radiation), full_load_hours, USER_INPUT()),
          UPDATE(V(energy_hydrogen_solar_pv_solar_radiation), number_of_units, original_nou_hydrogen_solar_pv),
          UPDATE(V(energy_hydrogen_solar_pv_solar_radiation), preset_demand_by_electricity_production, V(energy_hydrogen_solar_pv_solar_radiation, production_based_on_number_of_units)),
          UPDATE(V(energy_hydrogen_electrolysis_solar_electricity), full_load_hours, PRODUCT(USER_INPUT(), electrolyser_pv_flh_ratio)),
          UPDATE(V(energy_hydrogen_electrolysis_solar_electricity), number_of_units, original_nou_hydrogen_electrolysis_solar_pv),
          UPDATE(V(energy_hydrogen_electrolysis_solar_electricity), preset_demand_by_hydrogen_production, V(energy_hydrogen_electrolysis_solar_electricity, hydrogen_production_based_on_number_of_units)),
        )
      },
      -> {}
    )

- priority = 3
- max_value_gql = present:AREA(flh_solar_pv_solar_radiation_max)
- min_value_gql = present:V(energy_power_solar_pv_solar_radiation, full_load_hours)
- start_value_gql = present:V(energy_power_solar_pv_solar_radiation, full_load_hours)
- step_value = 1.0
- unit = hours
- update_period = future
