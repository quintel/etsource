# This query updates the following attributes:
# - edge value of share of busses in total demand passenger kms
# - number_of_units of busses, based on total passenger demand % change per year and modal split
# - AREA attribute number_of_busses, based on total passenger demand % change per year and modal split
# Priority is set to 2 since this query goes before transport_useful_demand_passenger_kms

- query =
    annual_change_demand_passenger_kms = DIVIDE(INPUT_VALUE(transport_useful_demand_passenger_kms), 100); 
    number_of_busses_present = QUERY_PRESENT(-> { AREA(number_of_busses)});   
    share_busses_in_demand_passenger_kms_present = QUERY_PRESENT(-> {V(transport_useful_demand_busses,share_of_transport_useful_demand_passenger_kms)});
    share_busses_in_demand_passenger_kms_future = DIVIDE(USER_INPUT(),100);
    number_of_busses_future = PRODUCT(RELATIVE_CHANGE_PER_YEAR(DIVIDE(number_of_busses_present,share_busses_in_demand_passenger_kms_present), annual_change_demand_passenger_kms),share_busses_in_demand_passenger_kms_future);

    EACH(
      UPDATE(
        EDGE(transport_useful_demand_busses, transport_useful_demand_passenger_kms),
        share,
        share_busses_in_demand_passenger_kms_future
      ),
      UPDATE(
        V(transport_useful_demand_busses), 
        number_of_units, 
        number_of_busses_future,
      ),
      UPDATE(
        AREA(),
        number_of_busses, 
        V(transport_useful_demand_busses, number_of_units)
      )
    )  

- share_group = passenger_transport
- priority = 2
- max_value = 100.0
- min_value = 0.0
- start_value_gql = present:V(transport_useful_demand_busses,share_of_transport_useful_demand_passenger_kms) * 100
- step_value = 0.1
- unit = %
- update_period = future
