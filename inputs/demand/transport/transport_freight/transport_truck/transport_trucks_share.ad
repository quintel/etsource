# This query updates the following attributes:
# - edge value of share of trucks in total demand freight tonne kms
# - number_of_units of trucks, based on % change per year in demand of total freight tonne kms demand and modal split
# - AREA attribute number_of_trucks, based on % change per year in demand of total freight tonne kms demand and modal split
# Priority is set to 2 since this query goes before transport_useful_demand_freight_tonne_kms

- query =
    annual_change_demand_freight_tonne_kms = DIVIDE(INPUT_VALUE(transport_useful_demand_freight_tonne_kms), 100);
    number_of_trucks_present = QUERY_PRESENT(-> { AREA(number_of_trucks)});
    share_trucks_in_demand_freight_tonne_kms_present = QUERY_PRESENT(-> {V(transport_useful_demand_trucks,share_of_transport_useful_demand_freight_tonne_kms)});
    share_trucks_in_demand_freight_tonne_kms_future = DIVIDE(USER_INPUT(),100);
    number_of_trucks_future = PRODUCT(RELATIVE_CHANGE_PER_YEAR(DIVIDE(number_of_trucks_present,share_trucks_in_demand_freight_tonne_kms_present), annual_change_demand_freight_tonne_kms),share_trucks_in_demand_freight_tonne_kms_future);

    EACH(
      UPDATE(
        EDGE(transport_useful_demand_trucks, transport_useful_demand_freight_tonne_kms),
        share,
        share_trucks_in_demand_freight_tonne_kms_future
      ),
      UPDATE(
        V(transport_useful_demand_trucks), 
        number_of_units, 
        number_of_trucks_future,
      ),
      UPDATE(
        AREA(),
        number_of_trucks, 
        V(transport_useful_demand_trucks, number_of_units)
      )
    )

- share_group = freight_transport
- priority = 2
- max_value = 100.0
- min_value = 0.0
- start_value_gql = present:V(transport_useful_demand_trucks,share_of_transport_useful_demand_freight_tonne_kms) * 100
- step_value = 0.1
- unit = %
- update_period = future
