# This input statement updates the useful refinery output.
# 
# ## Important Remarks:
# 
# 1. **Present Value Calculation**: The 'present_value' takes the preset_demand of the 
#    non-energetic crude oil demand of the refinery sector. Ideally we would take the 
#    useful output of node `industry_refinery_transformation_crude_oil`. This is not 
#    possible at the moment (see [https://github.com/quintel/etsource/issues/3298]).
# 
# 2. **Two Operation Modes**:
#    - **With refinery output in dataset**: This input will update the values with the 
#      relative value to update the heat and electricity input accordingly.
#    - **Without refinery output in dataset**: This input will assume that the input 
#      and output values of a 'typical refinery' are taken. 
# 
# For more information, please view: 
# `etdataset/inputs_source_analyses/demand/industry`

- query =
    present_value = QUERY_PRESENT(-> {Q(industry_useful_demand_for_chemical_refineries_crude_oil_non_energetic)});
    relative_value = USER_INPUT() / present_value;
    electricity_input_conversion_typical_refinery      = 0.00510;
    heat_input_conversion_typical_refinery             = 0.03607;
    refinery_gas_output_conversion_typical_refinery    = 0.03607;
    diesel_output_conversion_typical_refinery          = 0.40374;
    gasoline_output_conversion_typical_refinery        = 0.18022;
    kerosene_output_conversion_typical_refinery        = 0.07896;
    lpg_output_conversion_typical_refinery             = 0.02683;
    naphtha_output_conversion_typical_refinery         = 0.07404;
    heavy_fuel_oil_output_conversion_typical_refinery  = 0.10234;
    crude_oil_output_conversion_typical_refinery       = 0.09023;

    IF(
      present_value > 0.0,
    -> 
      {
        UPDATE_WITH_FACTOR(
          V(
            industry_useful_demand_for_chemical_refineries_coal_non_energetic,
            industry_useful_demand_for_chemical_refineries_crude_oil_non_energetic,
            industry_useful_demand_for_chemical_refineries_electricity,
            industry_useful_demand_for_chemical_refineries_network_gas_non_energetic,
            industry_useful_demand_for_chemical_refineries_wood_pellets_non_energetic,
            industry_useful_demand_for_chemical_refineries_useable_heat
          ),
          preset_demand,
          relative_value
        )
      },
    ->
      {
        EACH(
          UPDATE(
            V(industry_useful_demand_for_chemical_refineries_crude_oil_non_energetic),
            preset_demand,
            USER_INPUT() * BILLIONS
          ),
          UPDATE(
            V(industry_useful_demand_for_chemical_refineries_useable_heat),
            preset_demand,
            USER_INPUT() * heat_input_conversion_typical_refinery * BILLIONS
          ),
          UPDATE(
            V(industry_useful_demand_for_chemical_refineries_electricity),
            preset_demand,
            USER_INPUT() * electricity_input_conversion_typical_refinery * BILLIONS
          ),
          UPDATE(
            OUTPUT_SLOTS(V(industry_refinery_transformation_crude_oil),refinery_gas),
            conversion,
            refinery_gas_output_conversion_typical_refinery
          ),
          UPDATE(
            OUTPUT_SLOTS(V(industry_refinery_transformation_crude_oil),diesel),
            conversion,
            diesel_output_conversion_typical_refinery
          ),
          UPDATE(
            OUTPUT_SLOTS(V(industry_refinery_transformation_crude_oil),gasoline),
            conversion,
            gasoline_output_conversion_typical_refinery
          ),
          UPDATE(
            OUTPUT_SLOTS(V(industry_refinery_transformation_crude_oil),kerosene),
            conversion,
            kerosene_output_conversion_typical_refinery
          ),
          UPDATE(
            OUTPUT_SLOTS(V(industry_refinery_transformation_crude_oil),lpg),
            conversion,
            lpg_output_conversion_typical_refinery
          ),
          UPDATE(
            OUTPUT_SLOTS(V(industry_refinery_transformation_crude_oil),naphtha),
            conversion,
            naphtha_output_conversion_typical_refinery
          ),
          UPDATE(
            OUTPUT_SLOTS(V(industry_refinery_transformation_crude_oil),heavy_fuel_oil),
            conversion,
            heavy_fuel_oil_output_conversion_typical_refinery
          ),
          UPDATE(
            OUTPUT_SLOTS(V(industry_refinery_transformation_crude_oil),crude_oil),
            conversion,
            crude_oil_output_conversion_typical_refinery
          )
        ) 
      } 
    )

- priority = 1
- max_value_gql = present:PRODUCT(SUM(Q(final_demand_of_biomass_products),Q(final_demand_of_oil_and_derivatives)),10)
- min_value = 0.0
- start_value_gql = present:Q(industry_useful_demand_for_chemical_refineries_crude_oil_non_energetic)
- step_value = 0.1
- unit = PJ
- update_period = future
- disabled_by_couplings = [external_model_industry, industry_chemical_refineries]
