# This input sets the total installed electrical output capacity of Biomass CHPs (dispatchable)
# It distributes this total capacity according to temperature level (MT/HT) and CCS (with/without)

- query =
    mt_without_ccs_share = PRODUCT(DIVIDE(INPUT_VALUE(share_of_energy_chp_local_mt_wood_pellets_dispatchable),100.0),(1.0 - DIVIDE(INPUT_VALUE(share_of_energy_chp_local_mt_wood_pellets_ccs_dispatchable),100.0)));
    mt_with_ccs_share = PRODUCT(DIVIDE(INPUT_VALUE(share_of_energy_chp_local_mt_wood_pellets_dispatchable),100.0),DIVIDE(INPUT_VALUE(share_of_energy_chp_local_mt_wood_pellets_ccs_dispatchable),100.0));
    ht_without_ccs_share = PRODUCT(DIVIDE(INPUT_VALUE(share_of_energy_chp_local_ht_wood_pellets_dispatchable),100.0),(1.0 - DIVIDE(INPUT_VALUE(share_of_energy_chp_local_ht_wood_pellets_ccs_dispatchable),100.0)));
    ht_with_ccs_share = PRODUCT(DIVIDE(INPUT_VALUE(share_of_energy_chp_local_ht_wood_pellets_dispatchable),100.0),DIVIDE(INPUT_VALUE(share_of_energy_chp_local_ht_wood_pellets_ccs_dispatchable),100.0));


    EACH(
        UPDATE(
            V(energy_chp_local_mt_wood_pellets_dispatchable),
            number_of_units,
            mt_without_ccs_share * USER_INPUT() / V(energy_chp_local_mt_wood_pellets_dispatchable, electricity_output_capacity)
        ),
        UPDATE(
            L(energy_chp_local_mt_wood_pellets_dispatchable),
            preset_demand_by_electricity_production,
            V(energy_chp_local_mt_wood_pellets_dispatchable, production_based_on_number_of_units)
        ),
        UPDATE(
            V(energy_chp_local_mt_wood_pellets_ccs_dispatchable),
            number_of_units,
            mt_with_ccs_share * USER_INPUT() / V(energy_chp_local_mt_wood_pellets_ccs_dispatchable, electricity_output_capacity)
        ),
        UPDATE(
            L(energy_chp_local_mt_wood_pellets_ccs_dispatchable),
            preset_demand_by_electricity_production,
            V(energy_chp_local_mt_wood_pellets_ccs_dispatchable, production_based_on_number_of_units)
        ),
        UPDATE(
            V(energy_chp_local_ht_wood_pellets_dispatchable),
            number_of_units,
            ht_without_ccs_share * USER_INPUT() / V(energy_chp_local_ht_wood_pellets_dispatchable, electricity_output_capacity)
        ),
        UPDATE(
            L(energy_chp_local_ht_wood_pellets_dispatchable),
            preset_demand_by_electricity_production,
            V(energy_chp_local_ht_wood_pellets_dispatchable, production_based_on_number_of_units)
        ),
        UPDATE(
            V(energy_chp_local_ht_wood_pellets_ccs_dispatchable),
            number_of_units,
            ht_with_ccs_share * USER_INPUT() / V(energy_chp_local_ht_wood_pellets_ccs_dispatchable, electricity_output_capacity)
        ),
        UPDATE(
            L(energy_chp_local_ht_wood_pellets_ccs_dispatchable),
            preset_demand_by_electricity_production,
            V(energy_chp_local_ht_wood_pellets_ccs_dispatchable, production_based_on_number_of_units)
        )
    )

- priority = 0
- max_value_gql = present:2*DIVIDE(DIVIDE(Q(total_electricity_consumed), V(energy_chp_local_ht_wood_pellets_dispatchable,full_load_hours)), MJ_PER_MWH)
- min_value = 0.0
- start_value_gql = 
    present:SUM(
      PRODUCT(V(energy_chp_local_mt_wood_pellets_dispatchable,number_of_units),V(energy_chp_local_mt_wood_pellets_dispatchable,electricity_output_capacity)),
      PRODUCT(V(energy_chp_local_ht_wood_pellets_dispatchable,number_of_units),V(energy_chp_local_ht_wood_pellets_dispatchable,electricity_output_capacity))
    )
- step_value = 1.0
- unit = MW
- update_period = future
